autoload -Uz log_info log_status log_error log_debug log_warning

log_debug 'Checking for apt...'
if (( ! ${+commands[apt]} )) {
  log_error 'No apt command found - please install apt'
  return 2
} else {
  log_debug "Apt located at ${commands[apt]}"
}

local -a dependencies=("${(f)$(<${project_root}/.Aptfile)}")
local -a install_list
local binary

for dependency (${dependencies}) {
  local -a tokens=(${(s: :)dependency//(,|:|\')/})

  if [[ ! ${tokens[1]} == 'package' ]] continue

  # Install meson from PyPi to ensure more recent versions
  if [[ ${tokens[2]} == 'meson' && ${+commands[meson]} -ne 1 ]] {
    python3 -m pip install meson
    continue
  }

  if [[ ${#tokens} -gt 2 && ${tokens[3]} == 'bin' ]] {
    binary=${tokens[4]}
  } else {
    binary=${tokens[2]}
  }

  if (( ! ${+commands[${binary}]} )) install_list+=(${tokens[2]})
}

local -a _quiet=('' '--quiet')

log_debug "List of dependencies to install: ${install_list}"
if (( ${#install_list} )) {
  if (( ! ${+CI} )) log_warning 'Dependency installation via apt may require elevated privileges'

  sudo apt -y install ${install_list} ${_quiet[(( (_loglevel == 0) + 1 ))]}
}
